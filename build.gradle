plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'whisp'
version '2.0'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'whisp.server'
    mainClass = 'whisp.server.ServerApplication'
}

javafx {
    version = '17.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
    implementation 'org.postgresql:postgresql:42.7.2'
    implementation 'com.warrenstrange:googleauth:1.5.0'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'
    implementation 'com.google.zxing:core:3.5.2'
    implementation 'com.google.zxing:javase:3.5.2'
}

test {
    useJUnitPlatform()
}

tasks.register("prepareResources", Copy) {
    from(".") {
        include("server.truststore", "server.keystore", "ips.conf")
    }
    into("$buildDir/jpackage/WhispServer.app/Contents/app/conf")
}

tasks.named("jpackage").configure {
    dependsOn("prepareResources")
}

jlink {
    launcher {
        name = 'WhispServer'
    }

    jpackage {
        imageOptions = [
                '--icon', 'src/main/resources/whisp_logo.icns'
        ]
        options = ['--ignore-signing-information']
        installerOutputDir = file("$buildDir/installers")
        installerOptions = [
                '--name', 'WhispServer',
                '--app-version', '2.0',
                '--vendor', 'NelyPazos',
                '--dest', "$buildDir/installers",
                '--resource-dir', "$buildDir/jpackage/WhispServer.app/Contents/app/",
                '--type', 'dmg',
                '--mac-package-identifier', 'nelypazos.whispserver',
                '--mac-package-name', 'WhispServer',
        ]
    }
}
